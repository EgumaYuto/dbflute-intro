
// =======================================================================================
//                                                                           Core Settings
//                                                                           =============
buildscript {
    repositories {
        jcenter() // http://jcenter.bintray.com/
    }
}

plugins {
    id "com.moowork.gulp" version "0.11"
    id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'war'
apply plugin: 'project-report'


// =======================================================================================
//                                                                              Basic Info
//                                                                              ==========
group = 'org.dbflute'
version = '0.0.3-SNAPSHOT'

ext {
    encoding = 'UTF-8'

    versions =
        [
            // Basic Resource
            jdk : '1.8',
            gradle : '2.12',
            node : '5.9.1',

            // Main Framework
            dbflute : '1.1.1',
            h2 : '1.4.192',
            lastaflute : '0.8.4',

            // Partner Library
            slf4j : '1.7.18',
            logback : '1.1.6',
            jettyboot : '0.4.3',

            // GoodNeighbor Library
            commonslang : '3.4',
            commonsio : '2.4',

            // Testing
            junit : '4.8.2',
            utflute : '0.6.1B',
            javaparser : '2.2.2'
        ]
}


// =======================================================================================
//                                                                                 Plug-in
//                                                                                 =======
// TODO jflute intro: what is refreshResource? (Eclipse Synchronizer?)
task refreshResource {
}

task refreshResourceAfter {
}

// include plugin's gradle settings (using application settings, e.g. versions)
fileTree(dir: 'gradle/gradlePlugin', includes: ['*.gradle']).each { apply from: it }


// =======================================================================================
//                                                                                 Library
//                                                                                 =======
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/groups/public'
    }
}

dependencies {
    // Main Framework
    compile "org.dbflute:dbflute-runtime:${versions.dbflute}"
    runtime "com.h2database:h2:${versions.h2}"
    compile "org.lastaflute:lastaflute:${versions.lastaflute}"

    // Partner Library
    compile "javax.transaction:javax.transaction-api:1.2"
    compile "org.slf4j:slf4j-api:${versions.slf4j}"
    runtime "ch.qos.logback:logback-classic:${versions.logback}"
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "org.dbflute.jetty:jetty-boot:${versions.jettyboot}"

    // GoodNeighbor Library
    compile "org.apache.commons:commons-lang3:${versions.commonslang}"
    compile "commons-io:commons-io:${versions.commonsio}"

    // Testing
    testCompile "junit:junit:${versions.junit}"
    testCompile "org.dbflute.utflute:utflute-lastaflute:${versions.utflute}"
    testCompile "com.github.javaparser:javaparser-core:${versions.javaparser}"
}


// =======================================================================================
//                                                                               Packaging
//                                                                               =========
mainClassName = 'org.dbflute.intro.IntroBoot'

war {
    // TODO jflute intro: needs to remove servlet, jetty?
    from {
        configurations.providedCompile.collect {
            it.isDirectory() ? it : project.zipTree(it)
        }
        configurations.providedRuntime.collect {
            it.isDirectory() ? it : project.zipTree(it)
        }
    }

    from fileTree(dir: sourceSets.main.output.classesDir, includes: ["${mainClassName.replace('.', '/')}.class"])
    from fileTree(dir: file('dist'), include: '**/**')

    includeEmptyDirs = false

    version = ''

    def scmBranch = 'unknown'
    def scmRevision = 'unknown'

    try {
        scmBranch = 'git branch --contains'.execute().text.trim() - '* '
        scmRevision = 'git rev-parse HEAD'.execute().text.trim()
    } catch (Exception ignored) {
    }

    manifest {
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'SCM-Branch': scmBranch
        attributes 'SCM-Revision': scmRevision
        attributes 'Build-Timestamp': new java.text.SimpleDateFormat('yyyy/MM/dd HH:mm:ss.SSS').format(new Date())
        attributes 'Jenkins-Build-Number': System.getenv()['BUILD_NUMBER'] ?: ''
        attributes 'Main-Class' : mainClassName
    }
}


// =======================================================================================
//                                                                                 License
//                                                                                 =======
license {
    strictCheck true
    include "**/*.java"
    mapping {
        java='SLASHSTAR_STYLE'
    }
}


// =======================================================================================
//                                                                                 Refresh
//                                                                                 =======
task refresh(dependsOn: ['refreshResource', 'refreshResourceAfter', 'eclipse'])
tasks['refreshResourceAfter'].dependsOn(['refreshResource'])
if (tasks.findByName('eclipseClasspath')) {
    tasks['eclipseClasspath'].dependsOn(['refreshResource'])
}
